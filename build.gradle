plugins {
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.8'
    id 'java'
}

version = '0.0.1'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    group = 'com.example'
    sourceCompatibility = '11'
    repositories {
        maven { name "Alibaba"; url 'https://maven.aliyun.com/repository/public' }
    }
}

subprojects {
    dependencies {
        implementation 'org.apache.commons:commons-lang3'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation 'junit:junit'
        testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    }

    ext {
        set('snippetsDir', file("build/generated-snippets"))
        set('springCloudAlibabaVersion', "2.2.1.RELEASE")
        set('springCloudVersion', "Hoxton.SR4")
    }

    dependencyManagement {
        imports {
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    test {
        outputs.dir snippetsDir
        useJUnitPlatform()
    }

    asciidoctor {
        inputs.dir snippetsDir
        dependsOn test
    }

}





